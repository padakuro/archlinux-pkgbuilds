# Maintainer: Jesus Alvarez <jeezusjr at gmail dot com>
# Contributor: Kyle Fuller <inbox at kylefuller dot co dot uk>

pkgname=("zfs-utils${KERNEL_PACKAGE}-git")
pkgver=git
pkgrel=1
pkgdesc="Kernel module support files for the Zettabyte File System."
depends=(
  "spl${KERNEL_PACKAGE}-git=${SPL_PKG_VERSION}"
  "linux${KERNEL_PACKAGE}=${KERNEL_VERSION}"
)
makedepends=(
  "linux${KERNEL_PACKAGE}-headers=${KERNEL_VERSION}"
)
arch=("i686" "x86_64")
url="http://zfsonlinux.org/"
source=(
  'git+https://github.com/zfsonlinux/zfs.git'
  zfs-utils.bash-completion
  zfs-utils.initcpio.install
  zfs-utils.initcpio.hook
  zfs-utils.service
)
#groups=("archzfs")
sha512sums=(
  'SKIP'
  'e7ccb7179c4c455c496daab192099d7553674b9505801e25a2ea8c67a1b4de15d68ac99b2c3347c1f3454f88ede7694f08da61c5d061f0577bb1594ca4a90f12'
  'd5bd25217aecf001ea1e4bdc1f4a14073c8ece11d422369f8d49b5d672ba97ee99937d98c4dd9352e632aa15c002e991dd57122b545282360e3a681edecaf660'
  'cfbd5cd861ef5f7e11269685110474406935c51e3fa092f553b13c07fedcd6af096c1d94d865cebab23abadf35657d3ceb2a4239666bda84f469de9b6b66cb11'
  'cd57a1bbcc23f96b696a499749e018b9d0bb776be0b5e296719499aaf20cc82bb4d404ebeab44d99723dd712f84ee25bd0676940d126175f9997e8ca6e6f2c0b'
)
license=("CDDL")
conflicts=('zfs-utils')
provides=('zfs-utils')

pkgver() {
    echo "${ZFS_PKG_VERSION}"
}

build() {
  cd "$srcdir/zfs"
  ./autogen.sh
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --sbindir=/usr/sbin \
              --libdir=/usr/lib \
              --datadir=/usr/share \
              --includedir=/usr/include \
              --with-udevdir=/lib/udev \
              --libexecdir=/usr/lib/zfs \
              --with-config=user
  make
}

package() {
  cd "$srcdir/zfs"
  make DESTDIR="$pkgdir" install

  # Remove uneeded files
  rm -r $pkgdir/etc/init.d
  rm -r $pkgdir/usr/lib/dracut

  # move module tree /lib -> /usr/lib
  cp -r $pkgdir/{lib,usr}
  rm -r $pkgdir/lib

  install -D -m644 $srcdir/zfs-utils.initcpio.hook $pkgdir/usr/lib/initcpio/hooks/zfs
  install -D -m644 $srcdir/zfs-utils.initcpio.install $pkgdir/usr/lib/initcpio/install/zfs
  install -D -m644 $srcdir/zfs-utils.service $pkgdir/usr/lib/systemd/system/zfs.service
  install -D -m644 $srcdir/zfs-utils.bash-completion $pkgdir/usr/share/bash-completion/completions/zfs
}
