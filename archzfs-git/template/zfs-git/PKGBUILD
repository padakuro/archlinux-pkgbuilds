# Maintainer: Jesus Alvarez <jeezusjr at gmail dot com>
# Contributor: Kyle Fuller <inbox at kylefuller dot co dot uk>

pkgname=("zfs${KERNEL_PACKAGE}-git")
pkgver=git
pkgrel=1
pkgdesc="Kernel modules for the Zettabyte File System."
depends=(
  "spl${KERNEL_PACKAGE}-git=${SPL_PKG_VERSION}"
  "zfs-utils${KERNEL_PACKAGE}-git=${ZFS_PKG_VERSION}"
  "linux${KERNEL_PACKAGE}=${KERNEL_VERSION}"
)
makedepends=(
  "linux${KERNEL_PACKAGE}-headers=${KERNEL_VERSION}"
)
arch=("i686" "x86_64")
url="http://zfsonlinux.org/"
source=(
  'git+https://github.com/zfsonlinux/zfs.git'
)
#groups=("archzfs")
sha512sums=(
  'SKIP'
)
license=("CDDL")
install=zfs.install
conflicts=('zfs')
provides=('zfs')

pkgver() {
    echo "${ZFS_PKG_VERSION}"
}

build() {
  cd "$srcdir/zfs"
  ./autogen.sh
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --sbindir=/usr/sbin \
              --libdir=/usr/lib \
              --datadir=/usr/share \
              --includedir=/usr/include \
              --with-udevdir=/lib/udev \
              --libexecdir=/usr/lib/zfs \
              --with-config=kernel
  make
}

package() {
  cd "$srcdir/zfs"
  make DESTDIR="$pkgdir" install

  # move module tree /lib -> /usr/lib
  cp -r $pkgdir/{lib,usr}
  rm -r $pkgdir/lib
  
  # move modules from extra/ to extramodules/
  PKG_MODULE_DIR=$pkgdir/usr/lib/modules
  PKG_KERNEL_DIR=$(ls -1 $PKG_MODULE_DIR | head -1)
  mv $PKG_MODULE_DIR/$PKG_KERNEL_DIR/extra $PKG_MODULE_DIR/$PKG_KERNEL_DIR/extramodules
}
