# Maintainer: Jesus Alvarez <jeezusjr at gmail dot com>
# Contributor: Kyle Fuller <inbox at kylefuller dot co dot uk>

pkgname=("spl${KERNEL_PACKAGE}-git")
pkgver=git
pkgrel=1
pkgdesc="Solaris Porting Layer kernel modules."
depends=(
  "spl-utils${KERNEL_PACKAGE}-git=${SPL_PKG_VERSION}"
  "linux${KERNEL_PACKAGE}=${KERNEL_VERSION}"
)
makedepends=(
  "linux${KERNEL_PACKAGE}-headers=${KERNEL_VERSION}"
)
arch=("i686" "x86_64")
url="http://zfsonlinux.org/"
source=(
  'git+https://github.com/zfsonlinux/spl.git'
)
#groups=("archzfs")
sha512sums=(
  'SKIP'
)
license=("GPL")
install=spl.install
conflicts=('spl')
provides=('spl')

pkgver() {
    echo "${SPL_PKG_VERSION}"
}

build() {
  cd "$srcdir/spl"
  ./autogen.sh
  if [[ $CARCH == "i686" ]]; then
    ./configure --prefix=/usr --libdir=/usr/lib --sbindir=/usr/bin \
                --with-config=kernel --enable-atomic-spinlocks
  else
    ./configure --prefix=/usr --libdir=/usr/lib --sbindir=/usr/bin \
                --with-config=kernel
  fi
  make
}

package() {
  cd "$srcdir/spl"
  make DESTDIR=$pkgdir install

  # move module tree /lib -> /usr/lib
  cp -r $pkgdir/{lib,usr}
  rm -r $pkgdir/lib
  
  # move modules from extra/ to extramodules/
  PKG_MODULE_DIR=$pkgdir/usr/lib/modules
  PKG_KERNEL_DIR=$(ls -1 $PKG_MODULE_DIR | head -1)
  mv $PKG_MODULE_DIR/$PKG_KERNEL_DIR/extra $PKG_MODULE_DIR/$PKG_KERNEL_DIR/extramodules
}